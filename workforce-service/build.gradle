plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.6'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.crewvy'
version = '0.0.1-SNAPSHOT'
description = 'workforce'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	set('springCloudVersion', "2025.0.0")
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	// Eureka
	implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'

	// DB Driver
	runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'

	// Lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	// common 모듈을 의존성으로 추가
	implementation project(':common')

	// Kafka
	implementation 'org.springframework.kafka:spring-kafka'

	// Spring Cloud OpenFeign 의존성
	implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'

	// Spring Batch
	implementation 'org.springframework.boot:spring-boot-starter-batch'

	// QueryDSL
	implementation 'io.github.openfeign.querydsl:querydsl-jpa:5.6.1:jakarta'
	annotationProcessor "io.github.openfeign.querydsl:querydsl-apt:5.6.1:jakarta"
	annotationProcessor "jakarta.annotation:jakarta.annotation-api"
	annotationProcessor "jakarta.persistence:jakarta.persistence-api"
}

def querydslDir = layout.buildDirectory.dir("generated/querydsl").get().asFile

tasks.withType(JavaCompile).configureEach {
	options.encoding = 'UTF-8'
	options.generatedSourceOutputDirectory.set(querydslDir)
}

sourceSets {
	main.java.srcDirs += [ querydslDir ]
}

clean.doLast {
	file(querydslDir).deleteDir()
}

clean {
	delete file(querydslDir)
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

// 애플리케이션 실행 시 UTF-8 인코딩 강제
bootRun {
	systemProperty 'file.encoding', 'UTF-8'
}

tasks.named('test') {
	useJUnitPlatform()
}
